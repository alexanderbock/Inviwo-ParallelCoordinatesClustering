layout (local_size_variable) in;

layout (std430, binding = 0) readonly buffer Bins {
    int data[];
} bins;

layout (std430, binding = 1) buffer Identifiers {
    int nClusters;
    int data[];
} identifiers;

uniform int _nBins;
uniform int _nDimensions;
uniform int _selectedDimension;

bool isBinOccupied(int index, int dimension, const int nBins) {
    return (bins.data[dimension * nBins + index] == 1);
}

void main() {
    // We are going from the top to the bottom
    // Whenever we encounter a non-occupied bin, we reset the
    // current connected components
    identifiers.nClusters = 0;
    bool isInConnectedComponent = false;
    for (int i = 0; i < _nBins; ++i) {
        bool isOccupied = isBinOccupied(i, _selectedDimension, _nBins);

        if (isOccupied) {
            if (isInConnectedComponent) {
                identifiers.data[i] = identifiers.nClusters;
            }
            else {
                identifiers.nClusters += 1;
                
                identifiers.data[i] = identifiers.nClusters;
                isInConnectedComponent = true;
            }
        }
        else {
            identifiers.data[i] = -1;
            isInConnectedComponent = false;
        }
    }
}
